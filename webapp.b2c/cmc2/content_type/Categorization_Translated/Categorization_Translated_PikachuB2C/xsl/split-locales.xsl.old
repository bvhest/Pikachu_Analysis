<xsl:stylesheet version="2.0" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:template match="entry[@localisations]">
    <!-- selecting current node outside for-each loop to retain context -->
    <xsl:variable name="entry" select="."/>
    
    <xsl:for-each select="tokenize(@localisations, ' ')">
      <xsl:apply-templates select="$entry" mode="process">
        <xsl:with-param name="l" select="."/>
      </xsl:apply-templates>
    </xsl:for-each>
  </xsl:template>
  
  <xsl:template match="entry" mode="process">
    <xsl:param name="l"/>
    <xsl:copy>
      <xsl:apply-templates select="@*[name()!='localisations']"/>
      <xsl:attribute name="localisation"><xsl:value-of select="$l"/></xsl:attribute>
      <xsl:apply-templates select="node()"/>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template match="@*|node()">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
  </xsl:template>

</xsl:stylesheet>